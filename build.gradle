plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'de.blablubbabc'
version = '2.3.2'

repositories {
    mavenCentral()
    maven {
        name = 'spigot-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        name = 'vault-repo'
        url = 'https://jitpack.io'
    }
    maven {
        name = 'placeholder-api-repo'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = 'protocol-lib-repo'
        url = 'https://repo.dmulloy2.net/repository/public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.5.1'
}
shadowJar {
    classifier('')
    relocate('org.intellij.lang.annotations', 'de.blablubbabc.billboards.util.annotations.intellij')
    relocate('org.jetbrains.annotations', 'de.blablubbabc.billboards.util.annotations.jetbrains')
}
tasks.build.dependsOn(shadowJar)

compileJava {
    options.encoding = 'utf-8'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand('version': version)
        include 'plugin.yml'
    }
}
